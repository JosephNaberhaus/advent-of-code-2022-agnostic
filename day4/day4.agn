module DayFour

parseInt(value: string): int {
    var cur = 0;
    for (var i = 0; i < value.length; i++) {
        if (value[i] < '0' || value[i] > '9') {
            return 0;
        }

        cur *= 10;
        cur += int(value[i] - '0');
    }

    return cur;
}

split(s: string, sep: rune): list<string> {
    var result = make(list<string>);

    var cur = "";
    for (var i = 0; i < s.length; i += 1) {
        if (s[i] == sep) {
            result.push(cur);
            cur = "";
        } else {
            cur += string(s[i]);
        }
    }

    if (cur.length != 0) {
        result.push(cur);
    }

    return result;
}

model Section {
    start: int;
    end: int;

    contains(other: Section): bool {
        return start <= other.start && other.end <= end;
    }

    overlaps(other: Section): bool {
        return start <= other.end && other.start <= end;
    }
}

parseSection(str: string): Section {
    var splitStr = split(str, '-');

    var section = new Section();
    section.start = parseInt(splitStr[0]);
    section.end = parseInt(splitStr[1]);

    return section;
}

partOne(lines: list<string>): int {
    var count = 0;

    for (var line in lines) {
        var splitStr = split(line, ',');

        var firstSection = parseSection(splitStr[0]);
        var secondSection = parseSection(splitStr[1]);

        if (firstSection.contains(secondSection) || secondSection.contains(firstSection)) {
            count += 1;
        }
    }

    return count;
}

partTwo(lines: list<string>): int {
    var count = 0;

    for (var line in lines) {
        var splitStr = split(line, ',');

        var firstSection = parseSection(splitStr[0]);
        var secondSection = parseSection(splitStr[1]);

        if (firstSection.overlaps(secondSection)) {
            count += 1;
        }
    }

    return count;
}
